buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    }
}

allprojects {
    group 'com.tmtron.java-union'
    version '1.0-SNAPSHOT'
    description 'Annotation processor that builds a java union type'

    apply plugin: "com.github.hierynomus.license"
    license {
        header rootProject.file('LICENSE_HEADER')
        strictCheck true
        headerDefinitions {
            TMTRON_DOUBLESLASH_STYLE {
                firstLine = "//"
                endLine   = "//"
                firstLineDetectionPattern = "//"
                lastLineDetectionPattern  = "//"
                beforeEachLine = '// '
                allowBlankLines = false
                skipLinePattern = "//"
                isMultiline = false
            }
        }
        mapping {
            // NOTE: the DOUBLESLASH_STYLE does not work, because it has an extra empty line at the end
            java = 'TMTRON_DOUBLESLASH_STYLE'
        }
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'

    sourceCompatibility = 1.8

    dependencies {
        /* jsr305 includes a NonNull annotation
         * see also: http://stackoverflow.com/questions/4963300/which-notnull-java-annotation-should-i-use
         */
        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}